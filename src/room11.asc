// room script file;
int pisoAscensor = 0;

function actualizarDisplayPiso(int piso)
{
  oDispPiso0.Visible = (piso == 0);
  oDispPiso1.Visible = (piso == 1);
  oDispPisoP.Visible = (piso == -1);  
}

function subirPisos(int pisos)
{
  int i=0;
  while(i<pisos)
  {
    Wait(2*SEGUNDOS);
    pisoAscensor++;
    actualizarDisplayPiso(pisoAscensor);
    Wait(1*SEGUNDOS);
    i++;
  }  
}

function bajarPisos(int pisos)
{
  int i=0;
   while(i<pisos)
  {
    Wait(2*SEGUNDOS);
    pisoAscensor--;
    actualizarDisplayPiso(pisoAscensor);
    Wait(1*SEGUNDOS);
    i++;
  }
}

function trasladarsePisos(int pisos)
{
    if(pisos>=0)
      subirPisos(pisos);    
    else
      bajarPisos((-1)*pisos);
}

function setPisoAscensor(int piso)
{
  oAscLobby.Visible = (piso == 0);
  oAscPasillo.Visible = (piso == 1);
  oAscCochera.Visible = (piso == -1);
  pisoAscensor = piso;
}

function cerrarPuertasAscensor()
{
  if(puertaAscensorAbierta)
  {
    oPtaDcha.Move(375, oPtaDcha.Y, 1, eNoBlock, eAnywhere);  
    oPtaIzq.Move(260, oPtaIzq.Y, 1, eBlock, eAnywhere);
    RemoveWalkableArea(2);
    puertaAscensorAbierta = false;
  }
}

function abrirPuertasAscensor()
{
  if(!puertaAscensorAbierta)
  {
    oPtaDcha.Move(468, oPtaDcha.Y, 1, eNoBlock, eAnywhere);  
    oPtaIzq.Move(167, oPtaIzq.Y, 1, eBlock, eAnywhere);
    RestoreWalkableArea(2);
    puertaAscensorAbierta = true;
    SetTimer(1, 4*SEGUNDOS);
  }
}

function hBtnSubsuelo_Interact()
{
  cEgo.Say("Al auto... ");
  if(pisoAscensor != -1)
  {   
    cerrarPuertasAscensor();        
    trasladarsePisos((-1)-pisoAscensor); 
    setPisoAscensor(-1);
  }
  abrirPuertasAscensor();
}

function hBtn2doPiso_Interact()
{
   cEgo.ChangeRoom(ROOM_MDQ_DEPTO_ALDO, 1240, 360);
}

function hBtn1erPiso_Interact()
{
  cEgo.Say("A casa... ");
  if(pisoAscensor != 1)
  {   
    cerrarPuertasAscensor();        
    trasladarsePisos((1)-pisoAscensor); 
    setPisoAscensor(1);
  }
  abrirPuertasAscensor();
}

function hBtnPlantaBaja_Interact()
{
  cEgo.Say("Lobby... ");
  if(pisoAscensor != 0)
  {   
    cerrarPuertasAscensor();        
    trasladarsePisos((0)-pisoAscensor); 
    setPisoAscensor(0);
  }
  abrirPuertasAscensor();
}



function room_Load()
{
 
  if(cEgo.PreviousRoom == ROOM_MDQ_PASILLO_DEPTOS)
  {
    actualizarDisplayPiso(1);
    setPisoAscensor(1);
  }
  else if(cEgo.PreviousRoom == ROOM_MDQ_LOBBY)
  {
    actualizarDisplayPiso(0);
    setPisoAscensor(0);
  }
  else if(cEgo.PreviousRoom == ROOM_MDQ_COCHERA)
  {
    actualizarDisplayPiso(-1);
    setPisoAscensor(-1);
  }
}



function room_LeaveTop()
{
  if(pisoAscensor == 0)
    cEgo.ChangeRoom(ROOM_MDQ_LOBBY, 700,  355);
  else if(pisoAscensor == -1)
    cEgo.ChangeRoom(ROOM_MDQ_COCHERA, 275, 445);
  else if(pisoAscensor == 1)
    cEgo.ChangeRoom(ROOM_MDQ_PASILLO_DEPTOS, 420,  250);
}

function room_AfterFadeIn()
{
  Wait(1*SEGUNDOS);
  cerrarPuertasAscensor();
}

function room_RepExec()
{
  if(IsTimerExpired(1))
  {
    cerrarPuertasAscensor();
  }
}
